// CONFIGURE ALL SUBPROJECTS ///////////////////////////////////////////////////

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'java'
	apply plugin: 'org.xtend.xtend'
	apply plugin: 'maven-publish'
	apply plugin: "com.jfrog.artifactory"

	sourceCompatibility=javaVersion
	targetCompatibility=javaVersion

	configurations {
		all*.exclude group: 'log4j', module: 'log4j'
		provided {
			dependencies.all { dep ->
				configurations.default.exclude group: dep.group, module: dep.name
			}
		}
		compile.extendsFrom provided
		testCompile.extendsFrom provided
	}

	dependencies {
		provided "io.vertx:vertx-platform:${vertxVersion}"
		provided "io.vertx:vertx-hazelcast:${vertxVersion}"
		compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtendVersion}"
		compile "org.eclipse.xtext:org.eclipse.xtext.xbase.lib:${xtendVersion}"
		compile 'com.kimengi.util:xtend-vertx:+'
		testCompile 'com.kimengi.util:xtend-vertx-test:+'
        testCompile "junit:junit:${junitVersion}"
	}

	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven {
			url "${artifactory_url}/${artifactory_release_repo}"
			credentials {
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}        
		mavenLocal()
	}

	eclipse.project.name = "${parent.name}-${name}"

	task install( dependsOn: publishToMavenLocal )
	task export( dependsOn: artifactoryPublish)

	task sourceJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	tasks.withType(Javadoc) {
		if (JavaVersion.current().isJava8Compatible()) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}    

	artifactoryPublish {
		dependsOn sourceJar, javadocJar
	}

}

// CONFIGURE ARTIFACTORY ///////////////////////////////////////////////////////

artifactory {
	contextUrl = "${artifactory_url}"
	publish {
		defaults {
			publications ('mavenJava')
		}
		repository {
			repoKey = "${artifactory_release_repo}"
			username = "${artifactory_user}"
			password = "${artifactory_password}"
		}
	}
	resolve {  
		repository {
			repoKey = "${artifactory_release_repo}"
			username = "${artifactory_user}"
			password = "${artifactory_password}"
		}
	}
}    
